plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
}

group 'io.fabasoad'
version '0.2.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.apache.groovy:groovy-all:4.0.6'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/fabasoad/pojo"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

def taskPreCommit = tasks.register('pre-commit') {
    onlyIf {
        System.getenv('SKIP_FINALIZE') == null || System.getenv('SKIP_FINALIZE') == 'false'
    }
    doLast {
        exec {
            commandLine 'pre-commit', 'install', '--hook-type', 'pre-commit'
        }
        exec {
            commandLine 'pre-commit', 'install', '--hook-type', 'pre-push'
        }
    }
}

build.configure { finalizedBy taskPreCommit }

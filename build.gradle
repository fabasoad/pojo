import org.gradle.api.internal.TaskInternal

plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.53.0'
}

group = 'io.fabasoad'
version = '0.2.4'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'org.apache.groovy:groovy-all:4.0.28'
    testImplementation 'org.spockframework:spock-core:2.4-M6-groovy-4.0'
}

dependencyLocking {
    lockAllConfigurations()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/fabasoad/pojo"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

abstract class MakeInstallTask extends DefaultTask {
    private ExecOperations execOperations

    @Inject
    MakeInstallTask(ExecOperations execOperations) {
        this.execOperations = execOperations
    }

    @Override
    Spec<? super TaskInternal> getOnlyIf() {
        return {
            System.getenv('SKIP_FINALIZE') != 'true'
        }
    }

    @TaskAction
    void doTaskAction() {
        execOperations.exec {
            commandLine 'make', 'install'
        }
    }
}

def taskMake = tasks.register('make', MakeInstallTask)

build.configure { finalizedBy taskMake }
